#pragma kernel ClusterLightsCull
#pragma kernel ClearCounter

#define ClusterMaxLightCount 32

struct LightData
{
    float4 position;
    float4 spotDirection;
    float4 color;
};

struct AABB
{
    float3 Center;
    float3 Extent;
};

float4x4 _WorldToViewMatrix;

float _LightCount;
float4 _CameraData;//Near, Far
float4 _ClustersNumData;//ClustersNumX, ClustersNumY, ClustersNumXRcp, ClustersNumYRcp
float4 _ClusterSizeData;//ClusterSizeX, ClusterSizeY, ClusterSizeZ, ClusterSizeZRcp

RWTexture3D<uint2> _LightsCullTexture;
RWStructuredBuffer<uint> _LightIndexBuffer;
RWStructuredBuffer<uint> _LightStartOffsetCounter;
StructuredBuffer<LightData> _LightData;

groupshared uint gs_PunctualLightCount[16];
groupshared uint gs_PunctualLightList[16][32];

//https://lxjk.github.io/2018/03/25/Improve-Tile-based-Light-Culling-with-Spherical-sliced-Cone.html
bool TileBasedLightConeTest(float3 lightPosVS, float lightRadius, float3 tileCenterVec, float tileCos, float tileSin)
{
    float lightDist = length(lightPosVS);
    float3 lightVec = lightPosVS / lightDist;
    float lightConeSin = saturate(lightRadius / lightDist);
    float lightConeCos = sqrt(1.0 - lightConeSin * lightConeSin);

    float lightTileCos = dot(lightVec, tileCenterVec);
    float lightTileSin = sqrt(1 - lightTileCos * lightTileCos);
    //sum angle = light cone half angle + tile cone half angle
    float sumCos = (lightRadius > lightDist) ? -1.0 : (tileCos * lightConeCos - tileSin * lightConeSin);

    return lightTileCos >= sumCos;
}

bool AABBSphereTest(AABB aabb, float3 sphereCenter, float sphereRadius2)
{
    /*float3 distanceFromAABB = max(max(sphereCenter - AABBMax, AABBMin - sphereCenter), half3(0.0, 0.0, 0.0));
    return sphereRadius2 >= distanceFromAABB * distanceFromAABB;*/
    float3 sphereInAABB = max(abs(sphereCenter - aabb.Center) - aabb.Extent, 0.0);
    return sphereRadius2 >= dot(sphereInAABB, sphereInAABB);
}

float4 ConeToSphere(float3 coneOrigin, float3 coneForward, float coneRadius, float cosAngle)
{
    float4 boundingSphere;
    if(cosAngle > 0.07071)//halfAngle<PI/4
    {
        float sphereRadius = coneRadius * 0.5 / cosAngle;
        boundingSphere.xyz = coneOrigin + sphereRadius * coneForward;
        boundingSphere.w = sphereRadius;
    }
    else
    {
        float sinAngle = sqrt(1 - cosAngle * cosAngle);
        boundingSphere.xyz = coneOrigin + coneRadius * cosAngle * coneForward;
        boundingSphere.w = coneRadius * sinAngle;
    }
 
    return boundingSphere;
}

//https://bartwronski.com/2017/04/13/cull-that-cone/
bool ConeSphereTest(float3 coneOrigin, float3 coneForward, float coneRadius, float tanAngle, float3 sphereCenter, float sphereRadius)
{
    float3 V = sphereCenter.xyz - coneOrigin;
    float  VlenSq = dot(V, V);
    float  V1len  = dot(V, coneForward);
    float  distanceClosestPoint = sqrt(VlenSq - V1len*V1len) - V1len * tanAngle;
 
    bool angleCull = distanceClosestPoint > sphereRadius;
    bool frontCull = V1len >  sphereRadius + coneRadius;
    bool backCull  = V1len < -sphereRadius;
    return !(angleCull || frontCull || backCull);
}

bool ConePlaneTest(float3 coneOrigin, float3 coneForward, float coneRadius, float tanAngle, float4 plane)
{
    float3 V = cross(cross(plane.xyz, coneForward), coneForward);
    float3 closetPoint = coneOrigin + coneForward * coneRadius + V * (coneRadius * tanAngle);
    return dot(float4(closetPoint, 1.0), plane) >= 0 || dot(float4(closetPoint, 1.0), plane) >= 0;
}

AABB CalcClusterAABB(float3 sides[4], float tileZMin, float tileZMax)
{
    float3 p[8];
    p[0] = sides[0] * tileZMin;
    p[1] = sides[1] * tileZMin;
    p[2] = sides[2] * tileZMin;
    p[3] = sides[3] * tileZMin;
    p[4] = sides[0] * tileZMax;
    p[5] = sides[1] * tileZMax;
    p[6] = sides[2] * tileZMax;
    p[7] = sides[3] * tileZMax;
    float3 AABBMin = p[0];
    float3 AABBMax = p[0];
    for (int i = 1; i < 8; ++i)
    {
        AABBMin = min(AABBMin, p[i]);
        AABBMax = max(AABBMax, p[i]);
    }
    AABB aabb;
    aabb.Center = (AABBMax + AABBMin) * 0.5;
    aabb.Extent = (AABBMax - AABBMin) * 0.5;
    return aabb;
}

[numthreads(16,16,1)]
void ClusterLightsCull(uint3 groupID : SV_GroupID, uint3 groupThreadID : SV_GroupThreadID)
{
    uint groupIdx = groupThreadID.y * 16 + groupThreadID.x;
    if (groupIdx < 16)
    {
        gs_PunctualLightCount[groupIdx] = 0;
    }
    GroupMemoryBarrierWithGroupSync();

    float2 tileLD = groupID.xy * _ClustersNumData.zw;
    float2 tileRU = groupID.xy * _ClustersNumData.zw + _ClustersNumData.zw;
    float4 tile = float4(tileLD, tileRU) * 2.0 - 1.0;
    float4 posCS = tile * float4(_ClusterSizeData.x, _ClusterSizeData.y, _ClusterSizeData.x, _ClusterSizeData.y);

    float3 tileCenterVec = normalize(float3(posCS.xy + posCS.zw, 1.0));
    float3 sides[4];
    sides[0] = normalize(float3(posCS.xy, 1.0));
    sides[1] = normalize(float3(posCS.xw, 1.0));
    sides[2] = normalize(float3(posCS.zy, 1.0));
    sides[3] = normalize(float3(posCS.zw, 1.0));

    //calculate tile cone
    float tileCos = min(min(min(dot(tileCenterVec, sides[0])
        , dot(tileCenterVec, sides[1]))
        , dot(tileCenterVec, sides[2]))
        , dot(tileCenterVec, sides[3]));
    float tileSin = sqrt(1 - tileCos * tileCos);

    //loop through light list
    uint lightCount = _LightCount;
    for (uint lightIdx = groupIdx; lightIdx < lightCount; lightIdx += 256)
    {
        LightData light = _LightData[lightIdx];
        float3 lightPosWS = light.position.xyz;
        float lightRadius = light.position.w;

        //SpotLight
        float4 spotDirection = light.spotDirection;
        if(spotDirection.w > 0.0)
        {
            float4 boundingSphere = ConeToSphere(lightPosWS, spotDirection.xyz, lightRadius, spotDirection.w);
            lightPosWS = boundingSphere.xyz;
            lightRadius = boundingSphere.w;
        }

        float3 lightPosVS = mul(_WorldToViewMatrix, float4(lightPosWS, 1.0)).xyz;
        lightPosVS.z = -(lightPosVS.z);
        float lightZMin = lightPosVS.z - lightRadius;
        float lightZMax = lightPosVS.z + lightRadius;

        if (TileBasedLightConeTest(lightPosVS, lightRadius, tileCenterVec, tileCos, tileSin) &&
            lightZMin <= _CameraData.y &&
            lightZMax >= _CameraData.x)
        {
            //GetSlice
            uint sliceF = (uint)(lightZMin * _ClusterSizeData.w);
            sliceF = clamp(sliceF, 0, 15);
            uint sliceB = (uint)(lightZMax * _ClusterSizeData.w);
            sliceB = clamp(sliceB, 0, 15);

            float tileZMin = max(sliceF * _ClusterSizeData.z, _CameraData.x);
            float lightRadius2 = lightRadius * lightRadius;

            for (uint slice = sliceF; slice <= sliceB; ++slice)
            {
                float tileZMax = (slice + 1) * _ClusterSizeData.z;
                AABB aabb = CalcClusterAABB(sides, tileZMin, tileZMax);
                if (AABBSphereTest(aabb, lightPosVS, lightRadius2))
                {
                    uint oriCount;
                    InterlockedAdd(gs_PunctualLightCount[slice], 1, oriCount);
                    if (oriCount < ClusterMaxLightCount)
                    {
                        gs_PunctualLightList[slice][oriCount] = lightIdx;
                    }
                }

                tileZMin = tileZMax;
            }
        }
    }
    GroupMemoryBarrierWithGroupSync();

    uint slice = groupIdx;
    uint lightCountCulled = gs_PunctualLightCount[slice];
    lightCountCulled = min(lightCountCulled, ClusterMaxLightCount);
    
    uint curStartOffset;
    if (slice < 16)
    {
        InterlockedAdd(_LightStartOffsetCounter[0], lightCountCulled, curStartOffset);
    }
    GroupMemoryBarrierWithGroupSync();

    if (slice < 16)
    {
        for (uint i = 0; i < lightCountCulled; ++i)
        {
            uint lightIdx = gs_PunctualLightList[slice][i];
            _LightIndexBuffer[curStartOffset + i] = lightIdx;
        }

        _LightsCullTexture[uint3(groupID.xy, slice)] = uint2(curStartOffset, lightCountCulled);
    }
}

[numthreads(1,1,1)]
void ClearCounter()
{
    _LightStartOffsetCounter[0] = 0;
}